// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // if (zx == 1) sets x = 0
    Fanout16(in=zx, out=zx16);
    Not16(in=zx16, out=nzx16);
    And16(a=nzx16, b=x, out=x2);

    // if (nx == 1) sets x = !x
    Fanout16(in=nx, out=nx16);
    Xor16(a=nx16, b=x2, out=x3);

    // if (zy == 1) sets y = 0
    Fanout16(in=zy, out=zy16);
    Not16(in=zy16, out=nzy16);
    And16(a=nzy16, b=y, out=y2);

    // if (ny == 1) sets y = !y
    Fanout16(in=ny, out=ny16);
    Xor16(a=ny16, b=y2, out=y3);

    // if (f == 1)  sets out = x + y
    Add16(a=x3, b=y3, out=x3PlusY3);
    // if (f == 0)  sets out = x & y
    And16(a=x3, b=y3, out=x3AndY3);
    // selection based on f
    Mux16(a=x3AndY3, b=x3PlusY3, sel=f, out=XYAddedOrAnded);

    // if (no == 1) sets out = !out
    Fanout16(in=no, out=no16);
    Xor16(a=no16, b=XYAddedOrAnded, out=out);

    Xor16(a=no16, b=XYAddedOrAnded, out=outCopy);
    // if (out == 0) sets zr = 1, else 0
    Or16Way(in=outCopy, out=notZR);
    Not(in=notZR, out=zr);

    // if (out < 0) sets ng = 1, else 0
    SignIsNegative16(in=outCopy, out=ng);
}